apply plugin: 'kotlin'

apply plugin: 'java-library'

apply plugin: 'com.jfrog.artifactory'

apply plugin: 'maven-publish'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation "junit:junit:4.12"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.squareup.okhttp3:okhttp:3.12.1"
    implementation "commons-codec:commons-codec:1.10"
    implementation "io.vertx:vertx-core:3.7.0"
    implementation "io.vertx:vertx-lang-kotlin:3.7.0"
    implementation "org.slf4j:slf4j-simple:1.7.25"
}

sourceCompatibility = "8"
targetCompatibility = "8"

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def libraryGroupId = 'io.padium.utils'
def libraryArtifactId = 'utils'
def libraryVersion = '0.1.1'

publishing {
    publications {
        jar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId
            artifact("$buildDir/libs/${artifactId}.jar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.group == null || it.name == null || it.version == null) {
                        return
                    }
                    if(!(it instanceof ExternalModuleDependency)) {
                        return
                    }
                    if (it.group == "org.jetbrains.kotlin") {
                        return
                    }

                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://padium.io:6553/artifactory'
    publish {
        repository {
            repoKey = 'gradle-dev-local'

            try {
                username = artifactory_username
                password = artifactory_password
            } catch (MissingPropertyException e) {
                System.err.println("Please set \"artifactory_username\" and \"artifactory_password\" in \$HOME/.gradle/gradle.properties")
                throw e
            }
        }
        defaults {
            publications('jar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}
